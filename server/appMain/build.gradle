apply plugin: 'application'

version = '1.0'

// application configuration items.
mainClassName = 'net.canarymod.Main'
applicationDefaultJvmArgs = ['-Xms1024M', '-Xmx1024M']

configurations{
    plugin
    plugin.exclude group: 'net.canarymod'
}

dependencies {

    plugin project(':plugins:HelloWorldPlugin')
    plugin project(':plugins:LavaBombPlugin')
    plugin project(':plugins:HardModePlugin')

}

task addPlugins(type: Copy){
    into "$buildDir/plugins"
    from configurations.plugin
}

task addEula{
    def eulaDir = file("$buildDir/eula")
    outputs.dir eulaDir

    doLast{
        eulaDir.mkdirs()
        def prop = new Properties()
        println "eulaAgree exists? "+project.hasProperty("eulaAgree")
        if(project.hasProperty("eulaAgree")){
            prop.setProperty("eula", project.eulaAgree)
        } else{
            prop.setProperty("eula", "false")
        }

        def propFile = new File(eulaDir, "eula.txt")
        propFile.createNewFile()
        prop.store(propFile.newWriter(), null)
    }
}

// place the plugins in the plugins directory
applicationDistribution.from(addPlugins){
    into "plugins"
}

// generate the EULA in the top-level directory
applicationDistribution.from(addEula){
}