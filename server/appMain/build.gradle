apply plugin: 'application'
apply plugin: 'wrapper'

version = '1.0'

mainClassName = 'net.canarymod.Main'
applicationDefaultJvmArgs = ['-Xms1024M', '-Xmx1024M']

repositories {
    maven{
        name 'canaryMod'
        url "http://repo.visualillusionsent.net/repository/public"
    }

    mavenCentral()
}

configurations{
    plugin
    plugin.exclude group: 'net.canarymod'
}

dependencies {
    compile group: 'net.canarymod', name: 'CanaryMod', version:'1.8.0-1.2.0', classifier: 'shaded', transitive: false

    plugin project(':plugins:HelloWorldPlugin')
    plugin project(':plugins:LavaBombPlugin')

}

task addPlugins(type: Copy){
    into "$buildDir/plugins"
    from configurations.plugin
}

task addEula{
    def eulaDir = file("$buildDir/eula")
    outputs.dir eulaDir

    doLast{
        eulaDir.mkdirs()
        def prop = new Properties()
        println "eulaAgree exists? "+project.hasProperty("eulaAgree")
        if(project.hasProperty("eulaAgree")){
            prop.setProperty("eula", project.eulaAgree)
        } else{
            prop.setProperty("eula", "false")
        }

        def propFile = new File(eulaDir, "eula.txt")
        propFile.createNewFile()
        prop.store(propFile.newWriter(), null)
    }
}

applicationDistribution.from(addPlugins){
    into "plugins"
}

applicationDistribution.from(addEula){

}